engine {
  Include({"**"})

  applyTo("pom.xml") {
    ReplaceSnippet("<groupId>com.example</groupId>", `<groupId>#{#groupId.toLowerCase()}</groupId>`)
    ReplaceSnippet("spring-streaming-app", #artifactId.toLowerCase())
  }

  applyTo("src/main/resources/application.yaml") {
    ReplaceSnippet("Foo", #objectModelClassName)
    YTT()
  }

  if (#applicationRole == 'source') {
    Exclude({"**/FooProcessor.java", "**/FooSink.java"})
  }

  if (#applicationRole == 'processor') {
    Exclude({"**/FooSource.java", "**/FooSink.java"})
  }

  if (#applicationRole == 'sink') {
    Exclude({"**/FooSource.java", "**/FooProcessor.java"})
  }

  let modelObject = #objectModelClassName.toLowerCase() in {
    applyTo("src/main/java/com/example/stream/model/Foo.java") {
      ReplaceSnippet(text: "Foo", with: #objectModelClassName)
      RewritePath(regex: "src/(?<sourceset>.*)/java/(?<currentpackage>.*/)model(?<untouchedpath>.*)" , rewriteTo: `src/#{#sourceset}/java/#{#currentpackage}/model/#{#objectModelClassName}.java`)
    }
    applyTo("src/main/java/com/example/stream/functions/**.java") {
      ReplaceSnippet(text: "Foo", with: #objectModelClassName)
      ReplaceSnippet(text: "foo", with: #modelObject)
      RewritePath(regex: "src/(?<sourceset>.*)/java/(?<currentpackage>.*/)functions/Foo(?<untouchedpath>.*)" , rewriteTo: `src/#{#sourceset}/java/#{#currentpackage}/functions/#{#objectModelClassName}#{#untouchedpath}`)
    }
  }

  let packageDirectory = #packageName.replace(".", "/") in {
    applyTo("**/*.java") {
      ReplaceSnippet(text: "com.example.stream", with: #packageName)
      RewritePath(regex: "src/(?<sourceset>.*)/java/(?<currentpackage>.*/)stream(?<untouchedpath>.*)" , rewriteTo: `src/#{#sourceset}/java/#{#packageDirectory}/#{#untouchedpath}`)
    }
  }

  // Update the artifact name as needed
  applyTo("README.md") {
    ReplaceSnippet(text: "streaming-app", with: #artifactId.toLowerCase())
  }

  // The ContainerApp, update the artifact name and path as needed
  applyTo(".tanzu/config/streaming-app.yml") {
    ReplaceText({
          {text: "streaming-app", with: #artifactId.toLowerCase()},
          {text: "foo-broker", with: #msgBrokerName.toLowerCase()}})
    RewritePath(rewriteTo: `.tanzu/config/#{#artifactId.toLowerCase()}.yml`)
  }

  // The service binding
  let id = T(java.util.UUID).randomUUID().toString().replace("-","").substring(2,8) in {
    applyTo(".tanzu/config/service-binding-foo-broker.yml") {
          ReplaceText({
                {text: "streaming-app", with: #artifactId.toLowerCase()},
                {text: "foo-broker", with: #msgBrokerName.toLowerCase()}})
      OpenRewriteRecipe('org.openrewrite.yaml.ChangeValue', { oldKeyPath: "$.metadata.name", value: #msgBrokerName.toLowerCase() + '-' + #id.toString() })
      RewritePath(rewriteTo: `.tanzu/config/service-binding-#{#msgBrokerName.toLowerCase()}.yml`)
    }
  }

  // The service instance
  if (#applicationRole == 'source') {
    applyTo(".tanzu/config/rabbitmq-instance-foo-broker.yml") {
      ReplaceSnippet(text: "foo-broker", with: #msgBrokerName.toLowerCase())
      RewritePath(rewriteTo: `.tanzu/config/rabbitmq-instance-#{#msgBrokerName.toLowerCase()}.yml`)
    }
  }
  else {
    Exclude({".tanzu/config/rabbitmq-instance-foo-broker.yml"})
  }

  // Update the configuration id
  let uuid = T(java.util.UUID).randomUUID() in {
    applyTo("tanzu.yml") {
      OpenRewriteRecipe('org.openrewrite.yaml.ChangeValue', { oldKeyPath: "$.configuration.id", value: `config-#{#uuid.toString()}`})
    }
  }

  {
    Include({'**'})
    + InvokeFragment("build-wrapper-maven")
  }
  UniquePath(strategy: UseLast)

  // Create the provenance file
  Provenance()

}
